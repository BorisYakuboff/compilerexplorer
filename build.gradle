import static org.apache.tools.ant.taskdefs.condition.Os.*

plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.3.7'
    id "de.undercouch.download" version "3.4.2"
}

group 'com.compilerexplorer'
version '1.4-SNAPSHOT'

patchPluginXml {
    changeNotes """
      First fully working version
    """
}

sourceCompatibility = 1.8

def clionVersion = '2018.1.6'

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

intellij {
    localPath = '.gradle/ide/clion-' + clionVersion
    updateSinceUntilBuild = false // Disables updating since-build attribute in plugin.xml
}

task setupClion() {
    def urlPrefix = 'https://download.jetbrains.com/cpp/CLion-'
    def downloadPrefix = '.gradle/downloads/clion'
    def targetLocation = ''
    def ext = ''
    def tree = fileTree(downloadPrefix)
    if (isFamily(FAMILY_WINDOWS)) {
        ext = '.zip'
        targetLocation = '.gradle/ide/clion-' + clionVersion
        tree = zipTree(downloadPrefix + ext)
    }
    if (isFamily(FAMILY_UNIX)) {
        ext = '.tar.gz'
        targetLocation = '.gradle/ide'
        tree = tarTree(resources.gzip(downloadPrefix + ext))
    }
    if (!new File(targetLocation).exists()) {
        download {
            src urlPrefix + clionVersion + ext
            dest downloadPrefix + ext
        }
        copy {
            from tree
            into targetLocation
        }
    }
}

compileJava.dependsOn('setupClion')

publishPlugin {
    username intellijPublishUsername
    password intellijPublishPassword
}

runIde {
    jvmArgs = ['-Xms2048m', '-Xmx8192m']
}
